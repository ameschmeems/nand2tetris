// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
	Mux16(a=x, b=false, sel=zx, out=x0);
	Mux16(a=y, b=false, sel=zy, out=y0);
	Not16(in=x0, out=negx);
	Mux16(a=x0, b=negx, sel=nx, out=x1);
	Not16(in=y0, out=negy);
	Mux16(a=y0, b=negy, sel=ny, out=y1);
	Add16(a=x1, b=y1, out=addxy);
	And16(a=x1, b=y1, out=andxy);
	Mux16(a=andxy, b=addxy, sel=f, out=result);
	Not16(in=result, out=negresult);
	Mux16(a=result, b=negresult, sel=no, out[0..7]=out1, out[8..15]=out2);
	And16(a[0..7]=out1, a[8..15]=out2, b[0..7]=out1, b[8..15]=out2, out=out);
	Or8Way(in=out1, out=zr1);
	Or8Way(in=out2, out=zr2);
	Or(a=zr1, b=zr2, out=zr3);
	Not(in=zr3, out=zr);
	And16(a[0..7]=out1, a[8..15]=out2, b[0..14]=false, b[15]=true, out[15]=ng);
}